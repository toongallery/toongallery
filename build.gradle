plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco' // JaCoCo 플러그인 추가
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	implementation 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// bcrypt
    implementation 'at.favre.lib:bcrypt:0.10.2'

	// jwt
	compileOnly group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

	// querydsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// spring security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// test
	testImplementation platform('org.junit:junit-bom:5.9.1')
	testImplementation 'org.junit.jupiter:junit-jupiter'

	// cache
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.github.ben-manes.caffeine:caffeine'

//	// AWS S3
// Spring Cloud AWS 3.x (Spring Boot 3 호환) Spring 스타일로 AWS S3를 쉽게 다룰 수 있게 도와주는 스타터
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.3.0'

// AWS SDK BOM (버전 통일용 플랫폼)
	implementation platform('software.amazon.awssdk:bom:2.27.21')

// AWS SDK for S3 (파일 업로드/삭제 등) 실제 AWS S3 작업 처리
	implementation 'software.amazon.awssdk:s3'
}

jacoco {
	toolVersion = "0.8.10" // 최신 버전 사용
}

tasks.jacocoTestReport {
	dependsOn test

	reports {
		xml.required = false  // XML 리포트 비활성화
		csv.required = false  // CSV 리포트 비활성화
		html.required = true  // ✅ HTML 리포트 활성화
		html.outputLocation = layout.buildDirectory.dir("reports/jacoco/html") // HTML 저장 경로 설정
	}
}

tasks.jacocoTestCoverageVerification {
	dependsOn jacocoTestReport

	violationRules {
		rule {
			limit {
				minimum = 0.30 // 30% 이상이어야 빌드 성공
			}
		}
	}
}

tasks.named('check') {
	dependsOn tasks.jacocoTestCoverageVerification
}


tasks.named('test') {
	useJUnitPlatform()
}